#
# Collects news items using a Query against the Elasticsearch index for the website
#
prototype(Lelesys.News:ElasticsearchNewsCollector) < prototype(Neos.Fusion:Value) {
    @context.filterByCategories = ${q(node).is('[instanceof Lelesys.News:List]') ? q(node).property_array('filterByCategories') : null}
    categoryAndTagFilter = Lelesys.News:ElasticsearchCategoryAndTagFilter
    dateFilter = Lelesys.News:ElasticsearchDateFilter

    value = ${Search.query(newsFolderNode).nodeType('Lelesys.News:News')}
    value.@process.filterByCategoryAndTag = ${value.queryFilterMultiple(this.categoryAndTagFilter)}
    value.@process.filterByDate = ${request.arguments.year ? value.queryFilter('range', this.dateFilter) : value}
    value.@process.sort = ${configuration.sortOrder == 'ASC' ? value.sortAsc(configuration.sortProperty) : value.sortDesc(configuration.sortProperty)}
    value.@process.limit = ${value.limit(1000)}
}

#
# returns a raw array to use for category / tag filtering
#
prototype(Lelesys.News:ElasticsearchCategoryAndTagFilter) < prototype(Neos.Fusion:Value) {
    value = Neos.Fusion:RawArray {
        categories = ${filterByCategories != null ? (Type.isString(filterByCategories) ? String.split(filterByCategories) : (Type.isArray(filterByCategories) ? (Array.isEmpty(filterByCategories) == false ? filterByCategories : null) : null)) : null}
        tags = ${String.toString(request.arguments.tag) != '' ? String.toString(request.arguments.tag) : null}
    }
}

#
# returns a raw array to use for date filtering
#
prototype(Lelesys.News:ElasticsearchDateFilter) < prototype(Neos.Fusion:Value) {
    filterByDateStart = Neos.Fusion:Case {
        yearAndMonth {
            condition = ${request.arguments.year && request.arguments.month}
            renderer = ${Date.parse(request.arguments.month + '/' + request.arguments.year, 'm/Y')}
        }
        year {
            condition = ${request.arguments.year}
            renderer = ${Date.parse('01/' + request.arguments.year, 'm/Y')}
        }
        default {
            condition = true
            renderer = ${Date.now()}
        }
    }
    @context.filterByDateStart = ${this.filterByDateStart}
    @context.filterByDateEnd = ${request.arguments.month ? Date.add(this.filterByDateStart, 'P1M') : Date.add(this.filterByDateStart, 'P1Y')}

    value = Neos.Fusion:RawArray {
        dateTime = Neos.Fusion:RawArray {
            gte = ${Date.format(filterByDateStart, 'm/Y')}
            lt = ${Date.format(filterByDateEnd, 'm/Y')}
            format = 'MM/yyyy'
        }
    }
}
